#version 460

#extension GL_GOOGLE_include_directive: require
#extension GL_EXT_mesh_shader: require
#extension GL_EXT_control_flow_attributes : require
#extension GL_KHR_shader_subgroup_shuffle : require

const uint WORK_GROUP_SIZE = 8;

layout (local_size_x = WORK_GROUP_SIZE, local_size_y = WORK_GROUP_SIZE) in;

layout (triangles, max_vertices = 64, max_primitives = 98) out;

struct Payload {
	uint pindex;
	uint resolution;
};

taskPayloadSharedEXT Payload payload;

layout (push_constant) uniform NGFPushConstants {
	mat4 model;
	mat4 view;
	mat4 proj;
	int resolution;
};

layout (location = 0) out vec3 position[];
layout (location = 1) out vec2 uvs[];

vec4 project(vec3 p)
{
	return proj * view * model * vec4(p, 1.0f);
}

const uint FEATURE_SIZE = 20;
const uint ENCODING_LEVELS = 8;
const uint FFIN = FEATURE_SIZE + 3 * 2 * ENCODING_LEVELS;
const uint MSIZE = max(FFIN, 64);

// Neural network weights
layout (binding = 0) uniform isampler1D ctex;
layout (binding = 1) uniform sampler1D vtex;
layout (binding = 2) uniform sampler2D ftex;
layout (binding = 3) uniform sampler1D biases;
layout (binding = 4) uniform sampler2D w0;
layout (binding = 5) uniform sampler2D w1;
layout (binding = 6) uniform sampler2D w2;
layout (binding = 7) uniform sampler2D w3;

shared vec4 row[MSIZE];

#define leaky_relu(x) max(x, 0.01 * x)

vec3 eval(vec2 uv)
{
	float A[MSIZE];
	vec4 B[16];

	ivec4 complex = texelFetch(ctex, int(payload.pindex), 0);

	vec3 v0 = texelFetch(vtex, complex.x, 0).xyz;
	vec3 v1 = texelFetch(vtex, complex.y, 0).xyz;
	vec3 v2 = texelFetch(vtex, complex.z, 0).xyz;
	vec3 v3 = texelFetch(vtex, complex.w, 0).xyz;

	vec3 vertex = mix(mix(v0, v1, uv.y), mix(v3, v2, uv.y), uv.x);

	for (uint i = 0; i < FEATURE_SIZE; i++) {
		vec4 fv = texelFetch(ftex, ivec2(i, payload.pindex), 0);
		A[i] = mix(mix(fv.x, fv.y, uv.y), mix(fv.w, fv.z, uv.y), uv.x);
	}

	// Positional encoding
	const float powers[] = float[](1, 2, 4, 8, 16, 32, 64, 128);

	uint k = FEATURE_SIZE;
	for (uint i = 0; i < ENCODING_LEVELS; i++) {
		float p = powers[i];
		vec3 sin_v = sin(p * vertex);
		vec3 cos_v = cos(p * vertex);

		A[k++] = sin_v.x;
		A[k++] = sin_v.y;
		A[k++] = sin_v.z;

		A[k++] = cos_v.x;
		A[k++] = cos_v.y;
		A[k++] = cos_v.z;
	}

	// Network evaluation
	uint tid = gl_LocalInvocationID.x + gl_LocalInvocationID.y * gl_WorkGroupSize.x;

	// Layer 0
	for (uint i = 0; i < 16; i++) {
		// Load matrix row into shared memory
		if (tid == 0) {
			for (uint j = 0; j < FFIN; j++)
				row[j] = texelFetch(w0, ivec2(i, j), 0);
		}

		barrier();

		// Evaluate
		vec4 v = texelFetch(biases, int(i), 0);
		for (uint j = 0; j < FFIN; j++)
			v += A[j] * row[j];

		B[i] = leaky_relu(v);
	}

	// Layer 1
	for (uint i = 0; i < 16; i++) {
		uint k = i << 2;

		// Evaluate
		vec4 v = texelFetch(biases, int(i) + 16, 0);
		for (uint j = 0; j < 16; j++) {
			uint l = j << 2;
			vec4 v0 = texelFetch(w1, ivec2(i, l + 0), 0);
			vec4 v1 = texelFetch(w1, ivec2(i, l + 1), 0);
			vec4 v2 = texelFetch(w1, ivec2(i, l + 2), 0);
			vec4 v3 = texelFetch(w1, ivec2(i, l + 3), 0);
			vec4 s = B[j];

			v += s.x * v0 + s.y * v1 + s.z * v2 + s.w * v3;
		}

		vec4 lv = leaky_relu(v);
		A[k + 0] = lv.x;
		A[k + 1] = lv.y;
		A[k + 2] = lv.z;
		A[k + 3] = lv.w;
	}

	// Layer 2
	for (uint i = 0; i < 16; i++) {
		// Evaluate
		vec4 v = texelFetch(biases, int(i) + 32, 0);
		for (uint j = 0; j < 64; j++)
			v += A[j] * texelFetch(w2, ivec2(i, j), 0);

		vec4 lv = leaky_relu(v);

		// Fuse with the last layer
		vec4 wx = texelFetch(w3, ivec2(i, 0), 0);
		vec4 wy = texelFetch(w3, ivec2(i, 1), 0);
		vec4 wz = texelFetch(w3, ivec2(i, 2), 0);

		vertex.x += dot(wx, lv);
		vertex.y += dot(wy, lv);
		vertex.z += dot(wz, lv);
	}

	return vertex + texelFetch(biases, 3 << 6, 0).xyz;
}

void main()
{
	uint res = min(payload.resolution, WORK_GROUP_SIZE);

	uint trix = res - 1;
	uint triy = res - 1;

	if (gl_LocalInvocationIndex >= res * res)
		return;

	ivec2 local = ivec2(gl_LocalInvocationIndex / res, gl_LocalInvocationIndex % res);

	vec2 uv = vec2(local) / float(res - 1);
	vec3 v = 10 * eval(uv);

	SetMeshOutputsEXT(res * res, 2 * trix * triy);

	uint gli = gl_LocalInvocationIndex;

	uvs[gli] = uv;
	position[gli] = v;
	gl_MeshVerticesEXT[gli].gl_Position = project(v);

	if (local.x < trix && local.y < triy) {
		uint tri = 2 * (local.x + trix * local.y);
		gl_PrimitiveTriangleIndicesEXT[tri] = uvec3(gli, gli + 1, gli + res);
		gl_PrimitiveTriangleIndicesEXT[tri + 1] = uvec3(gli + 1, gli + res + 1, gli + res);
	}
}